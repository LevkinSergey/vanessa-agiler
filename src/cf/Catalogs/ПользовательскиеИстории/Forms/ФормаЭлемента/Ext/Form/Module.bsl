
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьПодсказку();
	ПодсветитьИмяFeatureФайла();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодсказку()
	ШаблонHTML =
	"<html>
	|<head>
	|  <style>
	|   p {
	|    font-family: 'Courier New'; 
	|    font-size: 12pt;
	|   }
	|  </style>
	| </head>	
	|<body>
	|<p>#Текст</p>
	|</body>
	|</html>";
	
	Текст = "Чтобы добиться цели #Цель, #Роль будет #ВыполнятьКлючевоеДействие и для этого он(она) хочет #НеобходимыйФункционал";
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		Текст = СтрЗаменить(Текст, "#Цель", "<strong>" + Объект.Владелец + "</strong>");		
	Иначе
		Текст = СтрЗаменить(Текст, "#Цель", "<strong>?</strong>");		
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Роль) Тогда
		Текст = СтрЗаменить(Текст, "#Роль", "<strong>" + Объект.Роль + "</strong>");		
	Иначе
		Текст = СтрЗаменить(Текст, "#Роль", "<strong>роль</strong>");		
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.КлючевоеДействие) Тогда
		Текст = СтрЗаменить(Текст, "#ВыполнятьКлючевоеДействие", "<strong>" + Объект.КлючевоеДействие + "</strong>");		
	Иначе
		Текст = СтрЗаменить(Текст, "#ВыполнятьКлючевоеДействие", "<strong>выполнять ключевое действие</strong>");		
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.НеобходимыйФункционал) Тогда
		Текст = СтрЗаменить(Текст, "#НеобходимыйФункционал", "<strong>" + Объект.НеобходимыйФункционал + "</strong>");		
	Иначе
		Текст = СтрЗаменить(Текст, "#НеобходимыйФункционал", "<strong>необходимый функционал</strong>");		
	КонецЕсли;
	
	Текст = СтрЗаменить(ШаблонHTML, "#Текст", Текст);
	
	Подсказка = Текст;
КонецПроцедуры

&НаКлиенте
Процедура ПодсветитьИмяFeatureФайла()
	Если Не ЗначениеЗаполнено(Объект.ИмяFeatureФайла) Тогда
		Возврат;			
	КонецЕсли;
	
	Файл = Новый Файл(Объект.ИмяFeatureФайла);
	
	Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ПроверкаСуществованияFeatureФайлаЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСуществованияFeatureФайлаЗавершение(Существует, ДополнительныеПараметры) Экспорт
	Если Существует Тогда
		Элементы.ИмяFeatureФайла.ЦветТекста = WebЦвета.Зеленый;
		
	Иначе
		Элементы.ИмяFeatureФайла.ЦветТекста = WebЦвета.Красный;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокФункционала()
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = Объект.НеобходимыйФункционал;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РольПриИзменении(Элемент)
	ОбновитьПодсказку();
КонецПроцедуры

&НаКлиенте
Процедура КлючевоеДействиеПриИзменении(Элемент)
	ОбновитьПодсказку();
КонецПроцедуры

&НаКлиенте
Процедура НеобходимыйФункционалПриИзменении(Элемент)
	ОбновитьПодсказку();
	ОбновитьЗаголовокФункционала();	
КонецПроцедуры

&НаКлиенте
Процедура ИмяFeatureФайлаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	НачатьЗапускПриложения(Новый ОписаниеОповещения("ОткрытьFeatureФайлЗавершение", ЭтотОбъект), Объект.ИмяFeatureФайла);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьFeatureФайлЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Файл открыт во внешней программе.";
	Сообщение.Сообщить();	
КонецПроцедуры

&НаКлиенте
Процедура ИмяFeatureФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ВыбратьFeatureФайлЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьFeatureФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы.Количество() > 0 Тогда
		Объект.ИмяFeatureФайла = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ОбщегоНазначениеКлиент.УбратьСКонцовПробелы(Объект.Наименование);
	
	ОпределитьПутьКFetureФайлу();
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПутьКFetureФайлу()
	Объект.ИмяFeatureФайла = ОбщегоНазначениеСервер.ПолучитьПутьКFeatureФайлу(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПодсветитьИмяFeatureФайла();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПодсветитьИмяFeatureФайла" Тогда
		ПодсветитьИмяFeatureФайла();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименованиеИзНеобходимогоФункционала(Команда)
	Объект.Наименование = Объект.НеобходимыйФункционал;
КонецПроцедуры


