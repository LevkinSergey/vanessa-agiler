
&НаКлиенте
Процедура Синхронизировать(Команда)
	Если ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.Настраиваемый") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вариант объединения ""Настраиваемый"" в разработке. Следите за обновлениями.";
		Сообщение.Сообщить();	
		
		
	ИначеЕсли ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.СозданиеFeatureФайла") Тогда
		СоздатьФичу();
		
	ИначеЕсли ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.СозданиеПользовательскойИстории") Тогда
		СоздатьПользовательскуюИсторию();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФичу() 
	ШаблонФичи = ОбщегоНазначениеКлиент.ПолучитьШаблонФичи();
	
	ПутьКФиче = ПолучитьЗначениеНастроек(ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Путь"));
	ПользовательскаяИстория = ПолучитьЗначениеНастроек(ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.ПользовательскаяИстория"));
	
	НетОшибок = Истина;
	ЗаполнитьШаблон(ПользовательскаяИстория, ШаблонФичи, НетОшибок);
	
	Если НетОшибок Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		
		ТекстовыйДокумент.УстановитьТекст(ШаблонФичи);
		
		ТекстовыйДокумент.НачатьЗапись(Новый ОписаниеОповещения("ВыгрузитьИзФормыЭлементаЗавершение", ЭтотОбъект), ПутьКФиче, КодировкаТекста.UTF8);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗначениеНастроек(Поле) 
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Поле", Поле);
	
	НайденыеСтроки = НастройкиСинхронизации.НайтиСтроки(ПараметрыОтбора);
	
	Возврат НайденыеСтроки[0].ЗначениеВAgiler;
КонецФункции

&НаКлиенте
Процедура СоздатьПользовательскуюИсторию()
	Если Не КолонкаВзятьИзAgiler() Тогда
		Возврат;		
	КонецЕсли;
	
	СоздатьПользовательскуюИсториюИЗаполнитьНастройкиСинхронизации();
КонецПроцедуры

&НаСервере
Процедура СоздатьПользовательскуюИсториюИЗаполнитьНастройкиСинхронизации()
	НастройкиСинхронизацииНаСервере = РеквизитФормыВЗначение("НастройкиСинхронизации");
	
	НоваяПользовательскаяИстория = Справочники.ПользовательскиеИстории.СоздатьЭлемент();
	
	НоваяПользовательскаяИстория.Владелец = Параметры.ВладелецНовогоЭлемента;
	
	Для Каждого Строка Из НастройкиСинхронизацииНаСервере Цикл
		Если Строка.Поле = Перечисления.ПоляСинхронизации.Путь Тогда
			НоваяПользовательскаяИстория.ПутьКФиче = Строка.ЗначениеВFeature;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.ПользовательскаяИстория Тогда
			НоваяПользовательскаяИстория.Заголовок = Строка.ЗначениеВFeature;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Персона Тогда
			НоваяПользовательскаяИстория.Роль = Строка.ЗначениеВFeature;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Функциональность Тогда
			НоваяПользовательскаяИстория.НеобходимыйФункционал = Строка.ЗначениеВFeature;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.КлючевоеДействие Тогда
			НоваяПользовательскаяИстория.КлючевоеДействие = Строка.ЗначениеВFeature;
			
		КонецЕсли;
	КонецЦикла;
	
	НоваяПользовательскаяИстория.Записать();
	
	Для Каждого Строка Из НастройкиСинхронизацииНаСервере Цикл
		Если Строка.Поле = Перечисления.ПоляСинхронизации.Номер Тогда
			Строка.ЗначениеВAgiler = НоваяПользовательскаяИстория.НомерФичи;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.ПользовательскаяИстория Тогда
			Строка.ЗначениеВAgiler = НоваяПользовательскаяИстория.Ссылка;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Путь Тогда
			Строка.ЗначениеВAgiler = НоваяПользовательскаяИстория.ПутьКФиче;
			
		КонецЕсли;
	КонецЦикла;

	ЗначениеВРеквизитФормы(НастройкиСинхронизацииНаСервере, "НастройкиСинхронизации");
КонецПроцедуры

&НаКлиенте
Функция КолонкаВзятьИзAgiler()
	Результат = Истина;
	
	Текст = "Не заполнены обязательные строки из колонки ""agile""";
	
	ПоляИсключения = Новый Массив;
	
	ПоляИсключения.Добавить(ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Номер"));
	ПоляИсключения.Добавить(ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Путь"));
	ПоляИсключения.Добавить(ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.ПользовательскаяИстория"));
	
	ПерваяСтрока = Истина;
	
	Для Каждого Строка Из НастройкиСинхронизации Цикл
		Если ЭтоПолеВИсключениях(Строка.Поле, ПоляИсключения) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ЗначениеВAgiler) Тогда
			Результат = Ложь;
			
			Текст = Текст + ?(ПерваяСтрока, ": ", "; ")  + Строка.Поле;
			
			ПерваяСтрока = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Текст = Текст + ". Синхронизация прервана!";
	
	Если Не Результат Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Сообщить();	
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПолеВИсключениях(ТекущееПоле, ПоляИсключения)
	Результат = Истина;
	
	ИскомоеПоле = ПоляИсключения.Найти(ТекущееПоле);
	Если ИскомоеПоле = Неопределено Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура НайтиFeatureБезНомера(Команда)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Выполняеться поиск фичи без номера, первая попавшаяся загружается сюда, для синхронизации";
	Сообщение.Сообщить();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СоздатьПоляНастроекСинхронизации();
	ЗагрузитьТекущийЭлементВНастройки(Параметры.ТекущийЭлемент);
КонецПроцедуры

&НаСервере
Процедура СоздатьПоляНастроекСинхронизации()
	НастройкиСинхронизацииНаСервере = РеквизитФормыВЗначение("НастройкиСинхронизации");
	
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.ПользовательскаяИстория, Справочники.ПользовательскиеИстории.ПустаяСсылка());
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.Путь, "");
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.Номер, "");
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.Персона, Справочники.Персоны.ПустаяСсылка());
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.Функциональность, Справочники.Функциональности.ПустаяСсылка());
	СоздатьПолеНастроекСинхронизации(НастройкиСинхронизацииНаСервере, Перечисления.ПоляСинхронизации.КлючевоеДействие, Справочники.КлючевыеДействия.ПустаяСсылка());
	
	ЗначениеВРеквизитФормы(НастройкиСинхронизацииНаСервере, "НастройкиСинхронизации");
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьЗначенияКолонок(Элемент)
	Если Элемент.ТекущийЭлемент.Имя = "НастройкиСинхронизацииЗначениеВAgiler" Тогда
		ТекущийТип = ТипЗнч(Элемент.ТекущиеДанные.ЗначениеВAgiler);
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТекущийТип);
		
		Элемент.ТекущийЭлемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
		Элемент.ТекущийЭлемент.ВыбиратьТип = Ложь;
		
		Если ТекущийТип = Тип("Строка") Тогда
			Элемент.ТекущийЭлемент.КнопкаВыбора = Истина;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеНастроекСинхронизации(Таблица, Поле, Значение)
	НоваяСтрока = Таблица.Добавить();
	
	НоваяСтрока.Поле = Поле;
	НоваяСтрока.ЗначениеВAgiler = Значение;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТекущийЭлементВНастройки(ТекущаяПользовательскаяИстория)
	Если Не ЗначениеЗаполнено(ТекущаяПользовательскаяИстория) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиСинхронизацииНаСервере = РеквизитФормыВЗначение("НастройкиСинхронизации");
	
	Для Каждого Строка Из НастройкиСинхронизацииНаСервере Цикл
		Если Строка.Поле = Перечисления.ПоляСинхронизации.ПользовательскаяИстория Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Путь Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория.ПутьКФиче;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Номер Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория.НомерФичи;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Персона Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория.Роль;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.Функциональность Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория.НеобходимыйФункционал;
			
		ИначеЕсли Строка.Поле = Перечисления.ПоляСинхронизации.КлючевоеДействие Тогда
			Строка.ЗначениеВAgiler = ТекущаяПользовательскаяИстория.КлючевоеДействие;
			
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(НастройкиСинхронизацииНаСервере, "НастройкиСинхронизации");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСинхронизацииПередНачаломИзменения(Элемент, Отказ)
	ОпределитьЗначенияКолонок(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСинхронизацииЗначениеВAgilerНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИзФормыЭлементаЗавершение(ЗаписанУспешно, ДополнительныеПараметры) Экспорт
	//
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШаблон(ТекущийЭлемент, ШаблонФичи, НетОшибок)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПользовательскиеИстории.НомерФичи,
	|	ПользовательскиеИстории.Заголовок,
	|	ПользовательскиеИстории.Роль,
	|	ПользовательскиеИстории.КлючевоеДействие,
	|	ПользовательскиеИстории.НеобходимыйФункционал
	|ИЗ
	|	Справочник.ПользовательскиеИстории КАК ПользовательскиеИстории
	|ГДЕ
	|	ПользовательскиеИстории.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТекущийЭлемент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		НетОшибок = Ложь;
		
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ШаблонФичи = СтрЗаменить(ШаблонФичи, "#Номер", ВыборкаДетальныеЗаписи.НомерФичи);
	ШаблонФичи = СтрЗаменить(ШаблонФичи, "#Заголовок", ВыборкаДетальныеЗаписи.Заголовок);
	ШаблонФичи = СтрЗаменить(ШаблонФичи, "#Роль", ВыборкаДетальныеЗаписи.Роль);
	ШаблонФичи = СтрЗаменить(ШаблонФичи, "#КлючевоеДействие", ВыборкаДетальныеЗаписи.КлючевоеДействие);
	ШаблонФичи = СтрЗаменить(ШаблонФичи, "#НеобходимыйФункционал", ВыборкаДетальныеЗаписи.НеобходимыйФункционал);
КонецПроцедуры

&НаКлиенте
Процедура ВариантОбъединенияПриИзменении(Элемент)
	ОпределитьРежимОбъединения();
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьРежимОбъединения() 
	Если ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.Настраиваемый") Тогда
		Для Каждого Строка Из НастройкиСинхронизации Цикл
			Строка.РежимОбъединения = Неопределено;
		КонецЦикла;
		
		ДоступностьКолонки = Истина;
		
	ИначеЕсли ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.СозданиеFeatureФайла") Тогда
		РежимОбъединения = ПредопределенноеЗначение("Перечисление.РежимыОбъединения.ВзятьИзAgiler");
		
		Для Каждого Строка Из НастройкиСинхронизации Цикл
			Строка.РежимОбъединения = РежимОбъединения;
		КонецЦикла;
		
		ДоступностьКолонки = Ложь;
		
	ИначеЕсли ВариантОбъединения = ПредопределенноеЗначение("Перечисление.ВариантыОбъединения.СозданиеПользовательскойИстории") Тогда
		РежимОбъединения = ПредопределенноеЗначение("Перечисление.РежимыОбъединения.ВзятьИзFeature");
		
		Для Каждого Строка Из НастройкиСинхронизации Цикл
			Строка.РежимОбъединения = РежимОбъединения;
		КонецЦикла;
		
		ДоступностьКолонки = Ложь;
		
	КонецЕсли;
	
	Элементы.НастройкиСинхронизацииРежимОбъединения.Доступность = ДоступностьКолонки;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьFeature(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Каталог = ОбщегоНазначениеСервер.ПолучитьКаталогПроекта(Параметры.ВладелецНовогоЭлемента);
	ДиалогВыбораФайла.Фильтр = "feature|*.feature";
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ВыбратьFeatureФайлЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьFeatureФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Фича = ПрочитатьФичу(ВыбранныеФайлы[0]);
	
	Для Каждого Строка Из НастройкиСинхронизации Цикл
		Если Строка.Поле = ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Путь") Тогда
			Строка.ЗначениеВFeature = Фича.Путь;
			
		ИначеЕсли Строка.Поле = ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.ПользовательскаяИстория") Тогда
			Строка.ЗначениеВFeature = Фича.Заголовок;
			
		ИначеЕсли Строка.Поле = ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Персона") Тогда
			Строка.ЗначениеВFeature = Фича.Роль;
			
		ИначеЕсли Строка.Поле = ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.Функциональность") Тогда
			Строка.ЗначениеВFeature = Фича.Функциональность;
			
		ИначеЕсли Строка.Поле = ПредопределенноеЗначение("Перечисление.ПоляСинхронизации.КлючевоеДействие") Тогда
			Строка.ЗначениеВFeature = Фича.КлючевоеДействие;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПрочитатьФичу(ВыбранныйФайл)
	Фича = ПодготовитьФичу();
	
	Фича.Путь = ВыбранныйФайл;
	
	ТекстФичи = Новый ЧтениеТекста(ВыбранныйФайл, КодировкаТекста.UTF8);
	
	Пробел = " ";
	
	ИскомыеКлючевыеСлова = Новый Массив;
	
	ИскомыеКлючевыеСлова.Добавить("Функционал");
	ИскомыеКлючевыеСлова.Добавить("Функциональность");
	ИскомыеКлючевыеСлова.Добавить("Как");
	ИскомыеКлючевыеСлова.Добавить("Я хочу");
	ИскомыеКлючевыеСлова.Добавить("Хочу");
	ИскомыеКлючевыеСлова.Добавить("Чтобы");
	
	ИскомыеКлючевыеСлова.Добавить("Функционал:");
	ИскомыеКлючевыеСлова.Добавить("Функциональность:");
	ИскомыеКлючевыеСлова.Добавить("Как:");
	ИскомыеКлючевыеСлова.Добавить("Я хочу:");
	ИскомыеКлючевыеСлова.Добавить("Хочу:");
	ИскомыеКлючевыеСлова.Добавить("Чтобы:");
	
	ТекущаяСтрока = ТекстФичи.ПрочитатьСтроку();
	Пока Не ТекущаяСтрока = Неопределено Цикл
		ТекущаяСтрока = УбратьЛишниеПробелыИТабы(ТекущаяСтрока);
		
		ПервыйПробел = СтрНайти(ТекущаяСтрока, " ",,, 1);
		ТекущееКлючевоеСлово = СокрЛП(Лев(ТекущаяСтрока, ПервыйПробел));
		
		ВторойПробел = СтрНайти(ТекущаяСтрока, " ",,, 2);
		ТекущееСловосочетаник = СокрЛП(Лев(ТекущаяСтрока, ВторойПробел));
		
		ОставшаясяЧасть = СокрЛП(Сред(ТекущаяСтрока, ПервыйПробел));
		
		Для Каждого ИскомоеКлючевоеСлово Из ИскомыеКлючевыеСлова Цикл
			Если ТекущееКлючевоеСлово = ИскомоеКлючевоеСлово Тогда
				Если Ложь
					Или ТекущееКлючевоеСлово = "Функционал"
					Или ТекущееКлючевоеСлово = "Функциональность"
					Или ТекущееКлючевоеСлово = "Функционал:"
					Или ТекущееКлючевоеСлово = "Функциональность:"
					Тогда
					
					Фича.Заголовок = ОставшаясяЧасть;
					
				ИначеЕсли Ложь
					Или ТекущееКлючевоеСлово = "Как"
					Или ТекущееКлючевоеСлово = "Как:"
					Тогда
					
					Фича.Роль = ОставшаясяЧасть;
					
				ИначеЕсли Ложь
					Или ТекущееКлючевоеСлово = "Хочу"
					Или ТекущееКлючевоеСлово = "Хочу:"
					Тогда
					
					Фича.Функциональность = ОставшаясяЧасть;
					
				ИначеЕсли Ложь
					Или ТекущееКлючевоеСлово = "Чтобы"
					Или ТекущееКлючевоеСлово = "Чтобы:"
					Тогда
					
					Фича.КлючевоеДействие = ОставшаясяЧасть;
					
				КонецЕсли;

			ИначеЕсли ТекущееСловосочетаник = ИскомоеКлючевоеСлово Тогда
				Если Ложь
					Или ТекущееКлючевоеСлово = "Я хочу"
					Или ТекущееКлючевоеСлово = "Я хочу:"
					Тогда
					
					Фича.Функциональность = ОставшаясяЧасть;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;

		//-
		
		ТекущаяСтрока = ТекстФичи.ПрочитатьСтроку();
	КонецЦикла;
	
	Возврат Фича;
КонецФункции

&НаКлиенте
Функция УбратьЛишниеПробелыИТабы(Знач Строка) 
	Строка = СтрЗаменить(Строка, Символы.Таб, "");
	Строка = СокрЛП(Строка);
	
	КоличествоПробелов = 0;
	Пробел = " ";
	НеразрывныйПробел = Символы.НПП;
	НоваяСтрока = "";
	
	Пока Не ПустаяСтрока(Строка) Цикл
		ТекущийСимвол = Лев(Строка, 1);
		Если Ложь
			Или ТекущийСимвол = Пробел
			Или ТекущийСимвол = НеразрывныйПробел
			Тогда
			
			КоличествоПробелов = КоличествоПробелов + 1;
			
		Иначе
			КоличествоПробелов = 0;	
			
		КонецЕсли;
		
		Если Не КоличествоПробелов > 1 Тогда
			НоваяСтрока = НоваяСтрока + ТекущийСимвол;
		КонецЕсли;
		
		Строка = Сред(Строка, 2);
	КонецЦикла;
	
	Возврат НоваяСтрока;
КонецФункции

&НаКлиенте
Функция ПодготовитьФичу()
	Фича = Новый Структура;
	
	Фича.Вставить("Заголовок");
	Фича.Вставить("Путь");
	Фича.Вставить("Роль");
	Фича.Вставить("Функциональность");
	Фича.Вставить("КлючевоеДействие");

	Возврат Фича;
КонецФункции















